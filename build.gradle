plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
//Spring Docs Set
// (2)
ext {
    set('snippetsDir', file("build/generated-snippets"))
    // snippetsDir : 테스트 실행시 생성되는 응답을 저장할 디렉토리 지정
}

// (3)
configurations {
    asciidoctorExtensions
    // asciidoctorExtensions : Asciidoctor의 확장 기능을 사용하기 위한 설정
}

// (6) test 할 때 snippetsDir에 생성된 응답을 asciidoctor로 변환하여 .adoc 파일 생성
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// (7) asciidoctor를 사용하기 위해서 asciidoctor task에 asciidoctorExtensions 설정
tasks.named('asciidoctor') {
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    dependsOn test
}

// (8) asciidoctor task 실행시 생성된 html 파일을 src/main/resources/static/docs 디렉토리에 카피
task copyDocument(type: Copy) {
    dependsOn asciidoctor            // (8-1)
    from file("${asciidoctor.outputDir}")   // (8-2)
    into file("src/main/resources/static/docs")   // (8-3)
}
bootJar {
    dependsOn copyDocument    // (10-1) bootJar 실행되기전 copyDocument task 실행
    from ("${asciidoctor.outputDir}") {  // (10-2) bootJar에 asciidoctor task에서 생성된 html 파일 추가
        into 'static/docs'     // (10-3) bootJar에 추가될 경로
    }
}

build {
    dependsOn copyDocument  // (9) 빌드되기전 copyDocument task 실행
}

//Jacoco Set
jacoco {
    // JaCoCo 버전
    toolVersion = '0.8.7'

//  테스트결과 리포트를 저장할 경로 변경
//  default는 "$/jacoco"
//  reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled false
        csv.enabled false

//  각 리포트 타입 마다 리포트 저장 경로 설정
//  html.destination file("$buildDir/jacocoHtml")
//  xml.destination file("$buildDir/jacoco.xml")
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/exception/**',
                    '**/utils/**',
                    'com/slog/controller/IndexRestController.class',
                    'com/slog/SlogApplication.class',
                    '**/domain/**'
            ])
        })
    }

    dependsOn test
}

jacocoTestCoverageVerification {
    dependsOn test

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/exception/**',
                    '**/utils/**',
                    'com/slog/controller/IndexRestController.class',
                    'com/slog/SlogApplication.class',
                    '**/domain/**'
            ])
        })
    }

    violationRules {
        rule {
            enabled = true // 활성화
            element = 'CLASS' // 클래스 단위로 커버리지 체크
            // includes = []

            // 라인 커버리지 제한을 80%로 설정
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            // 브랜치 커버리지 제한을 80%로 설정
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

        }
    }
}

test {
    useJUnitPlatform()

    jacoco {
        enabled = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }

    finalizedBy jacocoTestReport
}

task testCoverage(type: Test) {
    group 'verification'
    description 'Runs the unit tests with coverage'

    dependsOn(':test',
            ':jacocoTestReport',
            ':jacocoTestCoverageVerification')

    tasks['jacocoTestReport'].mustRunAfter(tasks['test'])
    tasks['jacocoTestCoverageVerification'].mustRunAfter(tasks['jacocoTestReport'])
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.jacoco:org.jacoco.core:0.8.7'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.security:spring-security-test'
}

jar {
    enabled = false
}
